varnishtest "Check that generic TLV IDs are sent properly"

#REQUIRE_VERSION=2.2

feature ignore_unknown_macro

haproxy h1 -conf {
    defaults
        mode http
        log global

    listen sender
        bind "fd@${feS}"
        server example ${h1_feR_addr}:${h1_feR_port} send-proxy-v2 proxy-v2-options 0xE1=%[str("foo")],0xE2

    listen receiver
        bind "fd@${feR}" accept-proxy

        # Check that the TLV value is set in the backend.
        http-request set-var(txn.custom_tlv_a) fc_pp_tlv(0xE1)
        http-after-response set-header proxy_custom_tlv_a %[var(txn.custom_tlv_a)]

        # Check that TLVs without an value are sent out.
        http-request set-var(txn.custom_tlv_b) fc_pp_tlv(0xE2)
        http-after-response set-header proxy_custom_tlv_b %[var(txn.custom_tlv_b)]

        http-request return status 200
} -start

client c1 -connect ${h1_feS_sock} {
    txreq -url "/"
    rxresp
    expect resp.http.proxy_custom_tlv_a == "foo"
    expect resp.http.proxy_custom_tlv_b == ""
} -run

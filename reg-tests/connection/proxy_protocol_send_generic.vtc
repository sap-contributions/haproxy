varnishtest "Check that generic TLV IDs are sent properly"

#REQUIRE_VERSION=2.2

feature ignore_unknown_macro

haproxy h1 -conf {
    defaults
        mode http
        log global

    listen sender
        bind "fd@${feS}"
        server example ${h1_feR_addr}:${h1_feR_port} send-proxy-v2 proxy-v2-options 0xE1=%[str("foo")],0xE2

    listen receiver
        bind "fd@${feR}" accept-proxy

        # Check that the TLV value is set in the backend.
        http-request set-var(txn.custom_tlv_a) fc_pp_tlv(0xE1)
        http-after-response set-header proxy_custom_tlv_a %[var(txn.custom_tlv_a)]

        # Check that TLVs without an value are sent out.
        http-request set-var(txn.custom_tlv_b) fc_pp_tlv(0xE2)
        http-after-response set-header proxy_custom_tlv_b %[var(txn.custom_tlv_b)]

        http-request set-tlv(0xE3) str("bar")
        http-request set-var(txn.custom_tlv_c) fc_pp_tlv(0xE3)
        http-after-response set-header proxy_custom_tlv_c %[var(txn.custom_tlv_c)]

        # Check that we can alter the TLV in the connection on http-request level.
        http-request set-tlv(0xE3) str("bar")
        http-request set-var(txn.custom_tlv_c) fc_pp_tlv(0xE3)
        http-after-response set-header proxy_custom_tlv_c %[var(txn.custom_tlv_c)]

        # Check that we can alter the TLV in the connection on tcp-content level.
        tcp-request content set-tlv(0xE4) str("bar")
        http-request set-var(txn.custom_tlv_d) fc_pp_tlv(0xE4)
        http-after-response set-header proxy_custom_tlv_d %[var(txn.custom_tlv_d)]

        # Check that we can overwrite an existing TLV.
        tcp-request content set-tlv(0xE5) str("bar")
        http-request set-var(txn.custom_tlv_e) fc_pp_tlv(0xE5)
        http-after-response set-header proxy_custom_tlv_e %[var(txn.custom_tlv_e)]

        # Check that we can move from a small to a medium pool with length 129.
        tcp-request content set-tlv(0xE6) str("                                                                                                                                 ")
        http-request set-var(txn.custom_tlv_f) fc_pp_tlv(0xE6)
        http-after-response set-header proxy_custom_tlv_f %[var(txn.custom_tlv_f),length]

        # Note that we do not check for an invalid TLV ID as that would result in an
        # parser error anway.

        http-request return status 200
} -start

client c1 -connect ${h1_feS_sock} {
    txreq -url "/"
    rxresp
    expect resp.http.proxy_custom_tlv_a == "foo"
    expect resp.http.proxy_custom_tlv_b == ""
    expect resp.http.proxy_custom_tlv_c == "bar"
    expect resp.http.proxy_custom_tlv_d == "bar"
    expect resp.http.proxy_custom_tlv_e == "bar"
    expect resp.http.proxy_custom_tlv_f == 129
} -run
